{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}


forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
(int) is_empty(tuple t) inline asm "ISNULL";
(int) bit_length(int x) inline asm "UBITSIZE";


(int) has_space(builder b) asm "1 BCHKBITSQ#";
(int) has_space_spec(builder b, int space) asm "BCHKBITSQ";


(slice, int, int) load_uint_spec(slice slc, int res, int length) {
  int readed = min(slc.slice_bits(), length);
  res = (res << readed) | slc~load_uint(readed);
  return (slc, res, readed);
} 

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  int value_length = bit_length(value);
  int flag_length = bit_length(flag);

  tuple stack = null();
  builder bld = begin_cell();

  slice slc = linked_list.begin_parse();

  int flag_length_minus_one = flag_length - 1;
  int mask = (1 << flag_length_minus_one) - 1; 

  (slc, int buffer, int buffer_length) = load_uint_spec(slc, 0, flag_length_minus_one);

  while (~ slc.slice_data_empty?()) {

    buffer = (buffer << 1) | slc~load_uint(1);

    if (buffer == flag) {
      if (~ bld.has_space_spec(value_length)) {
        stack~push_back(bld);
        bld = begin_cell();
      }
      bld~store_uint(value, value_length);
      (slc, buffer, buffer_length) = load_uint_spec(slc, 0, flag_length_minus_one);
    } else {
      int out_bit = buffer >> flag_length_minus_one;
      if (~ bld.has_space()) {
        stack~push_back(bld);
        bld = begin_cell();
      }
      bld~store_uint(out_bit, 1);
      buffer &= mask;
    }
  }

  repeat (slc.slice_depth()) {
    slc = slc~load_ref().begin_parse();
    if (buffer_length < flag_length_minus_one) {
      (slc, buffer, int readed) = load_uint_spec(slc, buffer, flag_length_minus_one - buffer_length);
      buffer_length += readed;
    }

    while (~ slc.slice_data_empty?()) {

      buffer = (buffer << 1) | slc~load_uint(1);

      if (buffer == flag) {
        if (~ bld.has_space_spec(value_length)) {
          stack~push_back(bld);
          bld = begin_cell();
        }
        bld~store_uint(value, value_length);
        (slc, buffer, buffer_length) = load_uint_spec(slc, 0, flag_length_minus_one);
      } else {
        int out_bit = buffer >> flag_length_minus_one;
        if (~ bld.has_space()) {
          stack~push_back(bld);
          bld = begin_cell();
        }
        bld~store_uint(out_bit, 1);
        buffer &= mask;
      }
    }
  }

  
  if (~ bld.has_space_spec(buffer_length)) {
    stack~push_back(bld);
    bld = begin_cell();
  }
  bld~store_uint(buffer, buffer_length);


  cell res = bld.end_cell();
  while (~ is_empty(stack)) {
    bld = stack~pop_back();
    bld = bld.store_ref(res);
    res = bld.end_cell();
  }

  return res;
}