{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}


forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
(int) is_empty(tuple t) inline asm "ISNULL"; 

(int) bit_length(int x) inline asm "UBITSIZE";

(slice, int) read_bit(slice slc) inline {
  if (slc.slice_data_empty?()) {
    while (~ slc.slice_empty?()) {
      slc = slc~load_ref().begin_parse();
      if (~ slc.slice_data_empty?()) {
        return slc.load_uint(1);
      }
    }
    return (slc, 2);
  }
  return slc.load_uint(1);
}

(slice, int, int) read_word(slice slc, int length) inline {
  int available = slc.slice_bits();
  if (length <= available) {
    int res = slc~load_uint(length);
    return (slc, res, 0);
  }

  length -= available;
  int res = slc~load_uint(available);

  repeat (slc.slice_depth()) {
    slc = slc~load_ref().begin_parse();
    available = slc.slice_bits();
    if (length <= available) {
      res = (res << length) | slc~load_uint(length);
      return (slc, res, 0);
    }
    length -= available;
    res = (res << available) | slc~load_uint(available);
  }
  return (slc, res, length);
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  tuple stack = null();

  builder bld = begin_cell();

  slice slc = linked_list.begin_parse();
  int value_length = bit_length(value);
  int flag_length = bit_length(flag);
  int mask = (1 << flag_length) - 1;

  (slc, int buff, int left) = slc.read_word(flag_length);

  while (left == 0) {
    if (buff == flag) {
        if (value_length + bld.builder_bits() > 1023) {
          stack~push_back(bld);
          bld = begin_cell();
        }
        bld~store_uint(value, value_length);

        (slc, buff, left) = slc.read_word(flag_length);
    } else {
      buff <<= 1;
      int out_bit = buff >> flag_length;  
      buff &= mask;

      if (bld.builder_bits() == 1023) {
        stack~push_back(bld);
        bld = begin_cell();
      }
      bld~store_uint(out_bit, 1);

      int bit = slc~read_bit();
      if (bit == 2) {
        left = 1;
        buff >>= 1;
      } else {
        left = 0;
        buff |= bit;
      }
    }
  }

  int readed = flag_length - left;

  if (readed + bld.builder_bits() > 1023) {
    stack~push_back(bld);
    bld = begin_cell();
  }
  bld~store_uint(buff, readed);

  cell res = bld.end_cell();
  while (~ is_empty(stack)) {
    bld = stack~pop_back();
    bld = bld.store_ref(res);
    res = bld.end_cell();
  }

  return res;
}

