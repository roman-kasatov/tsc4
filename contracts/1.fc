{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
forall X -> (X, X) swap (X a, X b) asm "SWAP";

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  tuple stack = null();
  stack~push_back(tree);
  while (~ stack.is_null()) {
    tuple next = null();
    while (~ stack.is_null()) {
      cell cur = stack~pop_back();
      if (cur.cell_hash() == hash) {
        return cur;
      }
      slice slc = cur.begin_parse();
      repeat (slc.slice_refs()) {
        next~push_back(slc~load_ref());
      }
    }
    (stack, next) = (next, stack);
  }
  return begin_cell().end_cell();
}
